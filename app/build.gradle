plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    //id 'com.google.gms.google-services'
    //id 'com.google.firebase.crashlytics'
    id 'com.chrisney.enigma'
    id 'androidx.navigation.safeargs.kotlin'
    id 'org.jlleitschuh.gradle.ktlint'
}

apply from: '../jacoco.gradle'


def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    defaultConfig {
        applicationId "com.jmarkstar.princestheatre"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {

            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental" : "true",
                        "room.expandProjection" : "true"
                ]
            }
        }

        buildConfigField("boolean", "enableEncryption", "true")
    }

    buildFeatures {
        dataBinding true
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            // v1 signature scheme makes our app vulnerable to Janus vulnerability on Android <7.0
            v1SigningEnabled false
            v2SigningEnabled true
        }
    }

    buildTypes {

        debug {
            applicationIdSuffix ".debug"
            debuggable true
            testCoverageEnabled true
            minifyEnabled false
        }

        release {
            minifyEnabled true
            debuggable true
            testCoverageEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->

        variant.outputs.all { output ->

            def builtType = variant.buildType.name
            def versionName = variant.versionName
            def versionCode = variant.versionCode
            outputFileName = "app-${builtType}-${versionName}-${versionCode}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lintOptions {
        lintConfig file("lint.xml")
        xmlReport false
        htmlReport true
    }
}

kapt {
    correctErrorTypes true
}

ktlint {
    android = true
    debug = true
    outputColorName = "RED"
    reporters {
        reporter "plain"
        reporter "checkstyle"

        customReporters {
            "html" {
                fileExtension = "html"
                dependency = "com.pinterest.ktlint:ktlint-reporter-html:$ktlint_version"
            }
        }
    }
}

//TODO: Setup enigma
enigma {
    enabled = true
//    injectFakeKeys = true
//    classes = ["com.jmarkstar.cheqdemoproj.common.security.Passphrases.java"]
}

hilt {
    enableTransformForLocalTests = true
}

dependencies {

    implementation ("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"){
        exclude group:"org.jetbrains.kotlin", module: "kotlin-stdlib-jdk7"
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.fragment:fragment-ktx:1.3.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_component_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_component_version"

    // viewmodel + lifedata
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$livecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$livecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$livecycle_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hilt_viewmodel_version"
    kapt "androidx.hilt:hilt-compiler:$hilt_viewmodel_version"

    // Network
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$ok_http_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$ok_http_version"
    implementation "com.facebook.fresco:fresco:$fresco_version"
    implementation "com.facebook.fresco:imagepipeline-okhttp3:$fresco_imagepipeline_okhttp3_version"

    // Database
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "net.zetetic:android-database-sqlcipher:$android_sqlcipher"
    implementation "androidx.sqlite:sqlite-ktx:$sqlite_ktx_version"

    implementation "androidx.security:security-crypto:$security_crypto_version"

    // DI - Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // App Logging and crashes
    implementation "com.jakewharton.timber:timber:$timber_version"
    implementation platform("com.google.firebase:firebase-bom:$firebase_BoM")
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // Test
    testImplementation "junit:junit:$junit_version"
    testImplementation "androidx.test:core:$testcore_version"
    testImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    testImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation "androidx.test:runner:$testRunner_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptTest "com.google.dagger:hilt-android-compiler:$hilt_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "com.jraska.livedata:testing-ktx:1.1.0"

    // Android Test
    androidTestImplementation "androidx.test:rules:1.3.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation "androidx.test:runner:$testRunner_version"
}